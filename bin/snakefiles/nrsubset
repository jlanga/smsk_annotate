rule nrsubset_split_pep:
    input:
        fai = transdecoder + "transdecoder.pep.fai"
    output:
        expand(
            nr + "chunks/transdecoder_{chunk_id}.tsv",
            chunk_id = [
                '{0:05d}'.format(x) 
                for x in range(0, config["number_of_chunks"]["nrsubset_blastp"])
            ]
        )
    params:
        number_of_chunks = config["number_of_chunks"]["nrsubset_blastp"]
    log:
        nr + "split_pep.log"
    benchmark:
        nr + "split_pep.json"
    shell:
        "split "
            "--number l/{params.number_of_chunks} "
            "--numeric-suffixes "
            "--suffix-length 5 "
            "--additional-suffix .tsv "
            "{input.fai} "
            "{nr}/chunks/transdecoder_ "
        "2> {log}"



rule nrsubset_blastp_chunk:
    input:
        pep = transdecoder + "transdecoder.pep",
        fai = transdecoder + "transdecoder.pep.fai",
        chunk = nr + "chunks/transdecoder_{chunk_id}.tsv",
        db = db + "subset_nr"
    output:
        tsv = nr + "blastp/transdecoder_{chunk_id}.tsv"
    log:
        nr + "blastp/transdecoder_{chunk_id}.log"
    benchmark:
        nr + "blastp/transdecoder_{chunk_id}.json"
    shell:
        "cut -f 1 {input.chunk} "
        "| xargs samtools faidx {input.pep} "
        "| blastp "
            "-db {input.db} "
            "-max_target_seqs 1 "
            "-outfmt 6 "
            "-evalue 1e-5 "
        "> {output.tsv} " 
        "2> {log}"



rule nrsubset_blastp_merge:
    input:
        expand(
            nr + "blastp/transdecoder_{chunk_id}.tsv",
            chunk_id = [
                '{0:05d}'.format(x) 
                for x in range(0, config["number_of_chunks"]["nrsubset_blastp"])
            ]
        )
    output:
        tsv = nr + "blastp.tsv"
    log:
        nr + "blastp_merge.log"
    benchmark:
        nr + "blastp_merge.json"
    shell:
        "cat {input} > {output} 2> {log}"