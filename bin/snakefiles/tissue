rule tissue_experimental_design:
    output:
        table = tissue + "experimental_design.tsv"
    threads:
        1
    log:
        tissue + "experimental_design.log"
    benchmark:
        tissue + "experimental_design.json"
    run:
        design_dict = {
            accession: [
                config["rna_pe"][accession]["condition"],
                "results/quant/" + accession]
                for accession in config["rna_pe"]
        }            
        with open(output.table, "w") as table:
            table.write("sample\ttissue\tpath\n")
            for accession in design_dict:
                table.write(
                    "\t".join(
                        [accession, design_dict[accession][0], design_dict[accession][1]]
                    ) + "\n"
                )



rule tissue_pseudoalign_sample:
    input:
        forward = qc + "{sample}_1.fq.gz",
        reverse = qc + "{sample}_2.fq.gz",
        index   = filtering + "filtered_transcriptome.idx"
    output:
        abundance_tsv = tissue + "{sample}/abundance.tsv",
        abundance_h5  = tissue + "{sample}/abundance.h5",
    params:
        outdir  = tissue + "{sample}/",
        params  = config["kallisto_params"]
    threads:
        24
    log:
        tissue + "{sample}/quant.log"
    benchmark:
        tissue + "{sample}/quant.json"
    shell:
        "kallisto quant "
            "--index={input.index} "
            "--output={params.outdir} "
            "--threads={threads} "
            "{params.params} "
            "<(pigz --decompress --stdout {input.forward}) "
            "<(pigz --decompress --stdout {input.reverse}) "
        "2> {log}"




rule tissue_normalised_tpms:
    input:
        tsv = expand(
            tissue + "{sample}/abundance.tsv",
            sample= config["rna_pe"]
        ),
        h5s = expand(
            tissue + "{sample}/abundance.h5",
            sample= config["rna_pe"]
        ),
        design = tissue + "experimental_design.tsv"
    output:
        rdata = tissue + "so.Rdata",
        tpms = tissue + "normalised_tpms.tsv"
    threads:
        1 
    log:
        tissue + "normalised_tpms.log"
    benchmark:
        tissue + "normalised_tpms.json"
    shell:
        "Rscript bin/get_normalised_tpms.R "
            "--experimental_design {input.design} "
            "--sleuth_object {output.rdata} "
            "--normalised_tpms {output.tpms} "
        "2> {log}"